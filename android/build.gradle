// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.7.2'
        classpath 'com.google.gms:google-services:4.4.2'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

apply from: "variables.gradle"

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task createDebugKeystore {
    doLast {
        def debugKeystore = file('debug.keystore')
        if (!debugKeystore.exists()) {
            println "Creating debug keystore for CI environment at: ${debugKeystore.absolutePath}"
            exec {
                commandLine 'keytool', '-genkey', '-v',
                    '-keystore', 'debug.keystore',
                    '-storepass', 'android',
                    '-alias', 'androiddebugkey',
                    '-keypass', 'android',
                    '-keyalg', 'RSA',
                    '-keysize', '2048',
                    '-validity', '10000',
                    '-dname', 'CN=Android Debug,O=Android,C=US'
            }
            println "Debug keystore created successfully."
        } else {
            println "Debug keystore already exists at: ${debugKeystore.absolutePath}"
        }
    }
}

// Make assemble tasks depend on creating debug keystore in CI
tasks.whenTaskAdded { task ->
    if (task.name.matches('assemble.*') && System.getenv('CI') != null) {
        task.dependsOn createDebugKeystore
    }
}